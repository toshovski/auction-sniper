sourceSets {
    acceptanceTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.acceptanceTest.output
    }
}
dependencies {
    acceptanceTestCompile configurations.testCompile
    acceptanceTestRuntime configurations.testRuntime
}
task acceptanceTest(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    mustRunAfter test
    reports.html.destination = file("$buildDir/reports/acceptanceTest")
}
check.dependsOn acceptanceTest

//
// eclipse plugin needs some extra configuration:
//
configurations{
    projectInternalDependencies
}
dependencies {
    projectInternalDependencies sourceSets.main.output
}
eclipse {
    classpath {
        plusConfigurations += [ configurations.acceptanceTestCompile, configurations.acceptanceTestRuntime ]
        minusConfigurations += [ configurations.projectInternalDependencies ]
    }
}

